import Mathlib.Analysis.Complex.Liouville
import Mathlib.Topology.UniformSpace.Ascoli
import RMT4.Basic
import RMT4.defs
import RMT4.hurwitz

open Set Function Metric UniformConvergence Complex

variable {Î¹ : Type*} {U K : Set â„‚} {z : â„‚} {F : Î¹ â†’ â„‚ â†’áµ¤[compacts U] â„‚} {Q : Set â„‚ â†’ Set â„‚}

@[simp] lemma union_compacts : â‹ƒâ‚€ compacts U = U :=
  subset_antisymm (Î» _ âŸ¨_, hK, hzâŸ© => hK.1 hz)
    (Î» z hz => âŸ¨{z}, âŸ¨singleton_subset_iff.2 hz, isCompact_singletonâŸ©, mem_singleton zâŸ©)

def UniformlyBoundedOn (F : Î¹ â†’ â„‚ â†’ â„‚) (U : Set â„‚) : Prop :=
  âˆ€ K âˆˆ compacts U, âˆƒ M > 0, âˆ€ z âˆˆ K, range (eval z âˆ˜ F) âŠ† closedBall 0 M

def UniformlyBoundedOn' (F : Î¹ â†’ â„‚ â†’ â„‚) (U : Set â„‚) : Prop :=
  âˆ€ K âˆˆ compacts U, âˆƒ Q, IsCompact Q âˆ§ âˆ€ i, MapsTo (F i) K Q

lemma uniformlyBoundedOn_iff_uniformlyBoundedOn' (F : Î¹ â†’ â„‚ â†’ â„‚) (U : Set â„‚) :
    UniformlyBoundedOn F U â†” UniformlyBoundedOn' F U := by
  constructor <;> intro h K hK
  Â· obtain âŸ¨M, -, hM2âŸ© := h K hK
    refine âŸ¨closedBall 0 M, isCompact_closedBall _ _, fun i z hz => ?_âŸ©
    exact hM2 z hz <| mem_range.mpr âŸ¨i, rflâŸ©
  Â· obtain âŸ¨Q, hQ1, hQ2âŸ© := h K hK
    obtain âŸ¨M, hMâŸ© := hQ1.isBounded.subset_closedBall 0
    refine âŸ¨M âŠ” 1, by simp, fun z hz y => ?_âŸ©
    rintro âŸ¨i, rflâŸ©
    have := hM (hQ2 i hz)
    simp at this
    simp [this]

lemma UniformlyBoundedOn.totally_bounded_at (h1 : UniformlyBoundedOn F U) (hz : z âˆˆ U) :
    TotallyBounded (range (Î» (i : Î¹) => F i z)) := by
  obtain âŸ¨M, _, hMâŸ© := h1 {z} âŸ¨singleton_subset_iff.2 hz, isCompact_singletonâŸ©
  have := hM z (mem_singleton z)
  exact totallyBounded_subset this (isCompact_closedBall 0 M).totallyBounded

lemma UniformlyBoundedOn.deriv (h1 : UniformlyBoundedOn F U) (hU : IsOpen U)
    (h2 : âˆ€ i, DifferentiableOn â„‚ (F i) U) :
    UniformlyBoundedOn (deriv âˆ˜ F) U := by
  rintro K âŸ¨hK1, hK2âŸ©
  obtain âŸ¨Î´, hÎ´, hâŸ© := hK2.exists_cthickening_subset_open hU hK1
  have e1 : cthickening Î´ K âˆˆ compacts U :=
    âŸ¨h, isCompact_of_isClosed_isBounded isClosed_cthickening hK2.isBounded.cthickeningâŸ©
  obtain âŸ¨M, hMp, hMâŸ© := h1 _ e1
  refine âŸ¨M / Î´, div_pos hMp hÎ´, ?_âŸ©
  rintro zâ‚€ hzâ‚€ w âŸ¨i, rflâŸ©
  simp only [mem_closedBall_zero_iff]
  refine norm_deriv_le_aux hÎ´ ?_ ?_
  Â· exact (h2 i).diffContOnCl_ball ((closedBall_subset_cthickening hzâ‚€ Î´).trans h)
  Â· rintro z hz
    have : z âˆˆ cthickening Î´ K :=
      sphere_subset_closedBall.trans (closedBall_subset_cthickening hzâ‚€ Î´) hz
    simpa using hM z this âŸ¨i, rflâŸ©

lemma UniformlyBoundedOn'.deriv (h1 : UniformlyBoundedOn' F U) (hU : IsOpen U)
    (h2 : âˆ€ i, DifferentiableOn â„‚ (F i) U) :
    UniformlyBoundedOn' (deriv âˆ˜ F) U := by
  rintro K âŸ¨hK1, hK2âŸ©
  obtain âŸ¨Î´, hÎ´, hâŸ© := hK2.exists_cthickening_subset_open hU hK1
  have e1 : cthickening Î´ K âˆˆ compacts U :=
    âŸ¨h, isCompact_of_isClosed_isBounded isClosed_cthickening hK2.isBounded.cthickeningâŸ©
  obtain âŸ¨Q, hQ1, hQ2âŸ© := h1 _ e1
  obtain âŸ¨M, hMâŸ© := hQ1.isBounded.subset_closedBall 0
  refine âŸ¨closedBall 0 (M / Î´), isCompact_closedBall _ _, ?_âŸ©
  intro i x hx
  simp only [mem_closedBall_zero_iff]
  refine norm_deriv_le_aux hÎ´ ?_ ?_
  Â· exact (h2 i).diffContOnCl_ball ((closedBall_subset_cthickening hx Î´).trans h)
  Â· rintro z hz
    have : z âˆˆ cthickening Î´ K :=
      sphere_subset_closedBall.trans (closedBall_subset_cthickening hx Î´) hz
    simpa using hM (hQ2 i this)

lemma UniformlyBoundedOn.equicontinuous_on
    (h1 : UniformlyBoundedOn F U)
    (hU : IsOpen U)
    (h2 : âˆ€ (i : Î¹), DifferentiableOn â„‚ (F i) U)
    (hK : K âˆˆ compacts U) :
    EquicontinuousOn F K := by
  apply (equicontinuous_restrict_iff _).mp
  have key := h1.deriv hU h2
  rintro âŸ¨z, hzâŸ©
  obtain âŸ¨Î´, hÎ´, hâŸ© := nhds_basis_closedBall.mem_iff.1 (hU.mem_nhds (hK.1 hz))
  obtain âŸ¨M, hMp, hMâŸ© := key (closedBall z Î´) âŸ¨h, isCompact_closedBall _ _âŸ©
  rw [equicontinuousAt_iff]
  rintro Îµ hÎµ
  refine âŸ¨Î´ âŠ“ Îµ / M, gt_iff_lt.2 (lt_inf_iff.2 âŸ¨hÎ´, div_pos hÎµ hMpâŸ©), Î» w hw i => ?_âŸ©
  simp
  have e1 : âˆ€ x âˆˆ closedBall z Î´, DifferentiableAt â„‚ (F i) x :=
    Î» x hx => (h2 i).differentiableAt (hU.mem_nhds (h hx))
  have e2 : âˆ€ x âˆˆ closedBall z Î´, â€–_root_.deriv (F i) xâ€– â‰¤ M :=
    Î» x hx => by simpa using hM x hx âŸ¨i, rflâŸ©
  have e3 : z âˆˆ closedBall z Î´ := mem_closedBall_self hÎ´.le
  have e4 : w.1 âˆˆ closedBall z Î´ := by simpa using (lt_inf_iff.1 hw).1.le
  rw [dist_eq_norm]
  refine ((convex_closedBall _ _).norm_image_sub_le_of_norm_deriv_le e1 e2 e4 e3).trans_lt ?_
  have : â€–z - w.valâ€– < Îµ / M := by
    have := (lt_inf_iff.1 hw).2
    rwa [dist_comm, Subtype.dist_eq, dist_eq_norm] at this
  convert mul_lt_mul' le_rfl this (norm_nonneg _) hMp
  field_simp [hMp.lt.ne.symm, mul_comm]

def ð“•K (U : Set â„‚) (Q : Set â„‚ â†’ Set â„‚) : Set (â„‚ â†’áµ¤[compacts U] â„‚) :=
    {f âˆˆ ð“— U | âˆ€ K âˆˆ compacts U, MapsTo f K (Q K)}

theorem isClosed_ð“•K (hU : IsOpen U) (hQ : âˆ€ K âˆˆ compacts U, IsCompact (Q K)) :
    IsClosed (ð“•K U Q) := by
  rw [ð“•K, setOf_and] ; apply (isClosed_ð“— hU).inter
  simp only [setOf_forall, MapsTo]
  apply isClosed_biInter ; intro K hK
  apply isClosed_biInter ; intro z hz
  apply (hQ K hK).isClosed.preimage
  exact ((UniformOnFun.uniformContinuous_eval_of_mem â„‚ (compacts U)
    (mem_singleton z) âŸ¨singleton_subset_iff.2 (hK.1 hz), isCompact_singletonâŸ©).continuous)

theorem uniformlyBoundedOn_ð“•K (hQ : âˆ€ K âˆˆ compacts U, IsCompact (Q K)) :
    UniformlyBoundedOn (Subtype.val : ð“•K U Q â†’ _) U := by
  rw [uniformlyBoundedOn_iff_uniformlyBoundedOn']
  exact fun K hK => âŸ¨Q K, hQ K hK, fun f => f.2.2 K hKâŸ©

theorem isCompact_ð“•K (hU : IsOpen U) (hQ : âˆ€ K âˆˆ compacts U, IsCompact (Q K)) :
    IsCompact (ð“•K U Q) := by
  rw [isCompact_iff_compactSpace]
  apply @ArzelaAscoli.compactSpace_of_closedEmbedding _ _ _ _ _ (Subtype.val : ð“•K U Q â†’ _) _
    (compacts U) (fun K hK => hK.2)
  Â· refine âŸ¨âŸ¨by tauto, fun f g => Subtype.extâŸ©, ?_âŸ©
    simpa [range, UniformOnFun.ofFun] using isClosed_ð“•K hU hQ
  Â· intro K hK
    exact UniformlyBoundedOn.equicontinuous_on (uniformlyBoundedOn_ð“•K hQ) hU (fun f => f.2.1) hK
  Â· intro K hK z hz
    refine âŸ¨Q K, hQ K hK, fun f => f.2.2 K hK hzâŸ©

theorem montel (hU : IsOpen U) (h1 : UniformlyBoundedOn F U) (h2 : âˆ€ i, DifferentiableOn â„‚ (F i) U) :
    TotallyBounded (range F) := by
  choose! M hM using h1
  have l1 : range F âŠ† ð“•K U (fun K => closedBall 0 (M K)) := by
    rintro f âŸ¨i, rflâŸ©
    exact âŸ¨h2 i, fun K hK z hz => (hM K hK).2 z hz <| mem_range.mpr (by simp)âŸ©
  apply totallyBounded_subset l1
  exact IsCompact.totallyBounded <| isCompact_ð“•K hU <| fun K _ => isCompact_closedBall _ _

#print axioms montel
