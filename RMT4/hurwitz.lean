import Mathlib.Analysis.Complex.LocallyUniformLimit
import RMT4.uniform
import RMT4.cindex

open Filter Topology Set Metric Uniformity

section filter

variable {P : Î± â†’ Prop} {p : Filter Î±} {Ï† : â„ â†’ Set Î±}

lemma mem_iff_eventually_subset (hp : p.HasBasis (Î» t : â„ => 0 < t) Ï†) (hÏ† : Monotone Ï†) :
    s âˆˆ p â†” (âˆ€á¶  t in ğ“[>] 0, Ï† t âŠ† s) := by
  rw [(nhdsWithin_hasBasis nhds_basis_closedBall (Ioi (0 : â„))).eventually_iff]
  simp_rw [hp.mem_iff, â† exists_prop, mem_inter_iff, mem_closedBall_zero_iff]
  refine existsâ‚‚_congr (Î» Îµ hÎµ => âŸ¨Î» h r h' => (hÏ† (le_of_abs_le h'.1)).trans h,
    Î» h => h âŸ¨Eq.le (abs_eq_self.mpr hÎµ.le), hÎµâŸ©âŸ©)

lemma eventually_nhds_iff_eventually_ball [PseudoMetricSpace Î±] :
  (âˆ€á¶  z in ğ“ zâ‚€, P z) â†” (âˆ€á¶  r in ğ“[>] 0, âˆ€ z âˆˆ ball zâ‚€ r, P z) :=
mem_iff_eventually_subset nhds_basis_ball (Î» _ _ => ball_subset_ball)

lemma eventually_nhds_iff_eventually_closed_ball [PseudoMetricSpace Î±] :
  (âˆ€á¶  z in ğ“ zâ‚€, P z) â†” (âˆ€á¶  r in ğ“[>] 0, âˆ€ z âˆˆ closedBall zâ‚€ r, P z) :=
mem_iff_eventually_subset nhds_basis_closedBall (Î» _ _ => closedBall_subset_closedBall)

end filter

section unifops

variable [NormedField ğ•œ] {F G : Î¹ â†’ Î± â†’ ğ•œ} {f g : Î± â†’ ğ•œ} {x y : ğ•œ} {Î· Î·' : â„}
-- variables {Î¹ Î± ğ•œ : Type*} [normed_field ğ•œ] {p : filter Î¹} [ne_bot p] {K s : set Î±} {m mf mg : â„}

lemma dist_inv_le_dist_div (hÎ· : 0 < Î·) (hÎ·' : 0 < Î·')
    (hx : x âˆ‰ ball (0 : ğ•œ) Î·) (hy : y âˆ‰ ball (0 : ğ•œ) Î·') :
    dist xâ»Â¹ yâ»Â¹ â‰¤ dist x y / (Î· * Î·') := by
  have h1 : x â‰  0 := by contrapose! hx; simp [hx, hÎ·]
  have h2 : y â‰  0 := by contrapose! hy; simp [hy, hÎ·']
  simp [dist_eq_norm] at hx hy
  rw [dist_eq_norm, inv_sub_inv h1 h2, norm_div, norm_mul, dist_comm, dist_eq_norm]
  exact div_le_div (norm_nonneg _) le_rfl (mul_pos hÎ· hÎ·') (mul_le_mul hx hy hÎ·'.le (norm_nonneg _))

lemma titi {p q : Filter ğ•œ} (hp : p âŠ“ ğ“ 0 = âŠ¥) (hq : q âŠ“ ğ“ 0 = âŠ¥) :
    map (Î» x : ğ•œ Ã— ğ•œ => (x.1â»Â¹, x.2â»Â¹)) (ğ“¤ ğ•œ âŠ“ (p Ã—Ë¢ q)) â‰¤ ğ“¤ ğ•œ := by
  obtain âŸ¨U, hU, V, hV, hUVâŸ© := inf_eq_bot_iff.mp hp
  obtain âŸ¨U', hU', V', hV', hUV'âŸ© := inf_eq_bot_iff.mp hq
  obtain âŸ¨Î·, hÎ·, hVâŸ© := Metric.mem_nhds_iff.mp hV
  obtain âŸ¨Î·', hÎ·', hV'âŸ© := Metric.mem_nhds_iff.mp hV'
  have hÎ·Î·' : 0 < Î· * Î·' := mul_pos hÎ· hÎ·'
  intro u hu
  obtain âŸ¨Îµ, hÎµ, huâŸ© := mem_uniformity_dist.mp hu
  rw [mem_map_iff_exists_image]
  refine âŸ¨_, inter_mem_inf (dist_mem_uniformity (mul_pos hÎµ hÎ·Î·')) (prod_mem_prod hU hU'), ?_âŸ©
  rintro z âŸ¨x, âŸ¨hx1, hx2âŸ©, rflâŸ©
  have hx'1 : x.1 âˆ‰ ball (0 : ğ•œ) Î· :=
    Î» h => (Set.nonempty_of_mem (mem_inter hx2.1 (hV h))).ne_empty hUV
  have hx'2 : x.2 âˆ‰ ball (0 : ğ•œ) Î·' :=
    Î» h => (Set.nonempty_of_mem (mem_inter hx2.2 (hV' h))).ne_empty hUV'
  refine hu ((dist_inv_le_dist_div hÎ· hÎ·' hx'1 hx'2).trans_lt ?_)
  convert (div_lt_div_right hÎ·Î·').mpr hx1
  field_simp [hÎ·.lt.ne.symm, hÎ·'.lt.ne.symm]

lemma uniform_ContinuousOn_inv {s : Set ğ•œ} (hs : ğ“Ÿ s âŠ“ ğ“ 0 = âŠ¥) :
    UniformContinuousOn (Î» x => xâ»Â¹) s := by
  simpa only [UniformContinuousOn, Tendsto, â† prod_principal_principal] using titi hs hs

example (hÎ· : 0 < Î·) : UniformContinuousOn (Î» x => xâ»Â¹) ((ball (0 : ğ•œ) Î·)á¶œ) := by
  apply uniform_ContinuousOn_inv
  simpa only [inf_comm, inf_principal_eq_bot, compl_compl] using ball_mem_nhds _ hÎ·

lemma TendstoUniformlyOn.inv (hF : TendstoUniformlyOn F f p s) (hf : ğ“Ÿ (f '' s) âŠ“ ğ“ 0 = âŠ¥) :
    TendstoUniformlyOn Fâ»Â¹ fâ»Â¹ p s := by
  have : ğ“áµ˜ (f '' s) âŠ“ ğ“ 0 = âŠ¥ := by
    rw [inf_comm] at hf âŠ¢
    exact UniformSpace.nhds_inf_uniform_nhds_eq_bot hf
  have h1 := lemma1 hF
  rw [tendstoUniformlyOn_iff_tendsto] at hF âŠ¢
  refine (Filter.map_mono (le_inf hF h1)).trans (titi hf this)

lemma lxyab {x y a b : ğ•œ} : x * a - y * b = (x - y) * a + y * (a - b) := by ring

lemma TendstoUniformlyOn.mul_of_le
    (hF : TendstoUniformlyOn F f p s) (hG : TendstoUniformlyOn G g p s)
    (hf : âˆ€á¶  i in p, âˆ€ x âˆˆ s, â€–F i xâ€– â‰¤ mf) (hg : âˆ€á¶  i in p, âˆ€ x âˆˆ s, â€–G i xâ€– â‰¤ mg) :
    TendstoUniformlyOn (F * G) (f * g) p s := by
  by_cases NeBot p
  case neg => simp at h; simp [h, TendstoUniformlyOn]
  case pos =>
    set Mf := |mf| + 1
    set Mg := |mg| + 1
    have hMf : 0 < Mf := by positivity
    have hMg : 0 < Mg := by positivity
    replace hf : âˆ€á¶  i in p, âˆ€ x âˆˆ s, â€–F i xâ€– â‰¤ Mf := by
      filter_upwards [hf] with i hF x hx using (hF x hx).trans ((le_abs_self mf).trans (lt_add_one _).le)
    replace hg : âˆ€á¶  i in p, âˆ€ x âˆˆ s, â€–G i xâ€– â‰¤ Mg := by
      filter_upwards [hg] with i hG x hx using (hG x hx).trans ((le_abs_self mg).trans (lt_add_one _).le)
    have h1 : âˆ€ x âˆˆ s, â€–g xâ€– â‰¤ Mg := by
      intro x hx
      refine le_of_tendsto ((continuous_norm.tendsto (g x)).comp (hG.tendsto_at hx)) ?_
      filter_upwards [hg] with i hg using hg x hx
    simp_rw [Metric.tendstoUniformlyOn_iff, dist_eq_norm] at hF hG âŠ¢
    intro Îµ hÎµ
    filter_upwards [hf, hF (Îµ / (2 * Mg)) (by positivity), hG (Îµ / (2 * Mf)) (by positivity)] with i hf hF hG x hx
    have h2 : â€–(f x - F i x) * g xâ€– < Îµ / 2 := by
      rw [norm_mul]
      by_cases g x = 0
      case pos => simp [h, half_pos hÎµ]
      case neg =>
        convert mul_lt_mul (hF x hx) (h1 x hx) (norm_pos_iff.mpr h) (by positivity) using 1
        simp only [div_mul, mul_div_cancel, hMg.ne.symm, Ne.def, not_false_iff]
    have h3 : â€–F i x * (g x - G i x)â€– < Îµ / 2 := by
      rw [norm_mul]
      by_cases F i x = 0
      case pos => simp [h, half_pos hÎµ]
      case neg =>
        convert mul_lt_mul' (hf x hx) (hG x hx) (norm_nonneg _) hMf using 1
        field_simp [hMf.ne.symm]; ring
    simp_rw [Pi.mul_apply, lxyab]
    exact (norm_add_le _ _).trans_lt (add_halves' Îµ â–¸ add_lt_add h2 h3)

lemma TendstoUniformlyOn.mul_of_bound
    (hF : TendstoUniformlyOn F f p s) (hG : TendstoUniformlyOn G g p s)
    (hf : âˆ€ x âˆˆ s, â€–f xâ€– â‰¤ mf) (hg : âˆ€ x âˆˆ s, â€–g xâ€– â‰¤ mg) :
    TendstoUniformlyOn (F * G) (f * g) p s := by
  have h1 : âˆ€á¶  i in p, âˆ€ x âˆˆ s, â€–F i xâ€– â‰¤ mf + 1 := by
    simp_rw [Metric.tendstoUniformlyOn_iff, dist_eq_norm] at hF
    filter_upwards [hF 1 zero_lt_one] with i hF x hx
    have : â€–F i xâ€– â‰¤ â€–f x - F i xâ€– + â€–f xâ€– := by
      simpa [â† norm_neg (F i x - f x)] using norm_add_le (F i x - f x) (f x)
    linarith [hF x hx, hf x hx]
  have h2 : âˆ€á¶  i in p, âˆ€ x âˆˆ s, â€–G i xâ€– â‰¤ mg + 1 := by
    simp_rw [Metric.tendstoUniformlyOn_iff, dist_eq_norm] at hG
    filter_upwards [hG 1 zero_lt_one] with i hG x hx
    have : â€–G i xâ€– â‰¤ â€–g x - G i xâ€– + â€–g xâ€– := by
      simpa [â† norm_neg (G i x - g x)] using norm_add_le (G i x - g x) (g x)
    linarith [hG x hx, hg x hx]
  exact hF.mul_of_le hG h1 h2

variable [TopologicalSpace Î±]

lemma TendstoUniformlyOn.inv_of_compact (hF : TendstoUniformlyOn F f p K)
    (hf : ContinuousOn f K) (hK : IsCompact K) (hfz : âˆ€ x âˆˆ K, f x â‰  0) :
    TendstoUniformlyOn Fâ»Â¹ fâ»Â¹ p K := by
  apply hF.inv
  rw [inf_comm, inf_principal_eq_bot]
  exact (hK.image_of_continuousOn hf).isClosed.compl_mem_nhds (Î» âŸ¨z, h1, h2âŸ© => hfz z h1 h2)

lemma TendstoUniformlyOn.mul_of_compact
    (hF : TendstoUniformlyOn F f p K) (hG : TendstoUniformlyOn G g p K)
    (hf : ContinuousOn f K) (hg : ContinuousOn g K) (hK : IsCompact K) :
    TendstoUniformlyOn (F * G) (f * g) p K := by
  by_cases K = âˆ…
  case pos => simpa only [h] using tendstoUniformlyOn_empty
  case neg =>
    replace h : K.Nonempty := Set.nonempty_iff_ne_empty.2 h
    have h2 : ContinuousOn (norm âˆ˜ f) K := continuous_norm.comp_continuousOn hf
    have h3 : ContinuousOn (norm âˆ˜ g) K := continuous_norm.comp_continuousOn hg
    obtain âŸ¨xf, _, h4âŸ© : âˆƒ x âˆˆ K, âˆ€ y âˆˆ K, â€–f yâ€– â‰¤ â€–f xâ€– := hK.exists_forall_ge h h2
    obtain âŸ¨xg, _, h5âŸ© : âˆƒ x âˆˆ K, âˆ€ y âˆˆ K, â€–g yâ€– â‰¤ â€–g xâ€– := hK.exists_forall_ge h h3
    exact hF.mul_of_bound hG h4 h5

lemma TendstoUniformlyOn.div_of_compact
    (hF : TendstoUniformlyOn F f p K) (hG : TendstoUniformlyOn G g p K)
    (hf : ContinuousOn f K) (hg : ContinuousOn g K) (hgK : âˆ€ z âˆˆ K, g z â‰  0) (hK : IsCompact K) :
    TendstoUniformlyOn (F / G) (f / g) p K := by
  simpa [div_eq_mul_inv] using hF.mul_of_compact (hG.inv_of_compact hg hK hgK) hf (hg.invâ‚€ hgK) hK

end unifops

variable {F : Î¹ â†’ â„‚ â†’ â„‚} {f : â„‚ â†’ â„‚}
--   {U s : set â„‚} {r : â„}

lemma Filter.Eventually.exists' {P : â„ â†’ Prop} {tâ‚€} (h : âˆ€á¶  t in ğ“[>] tâ‚€, P t) :
    âˆƒ t > tâ‚€, P t := by
  simpa [and_comm, exists_prop] using (frequently_nhdsWithin_iff.mp h.frequently).exists

lemma order_eq_zero_iff {p : FormalMultilinearSeries â„‚ â„‚ â„‚}
    (hp : HasFPowerSeriesAt f p zâ‚€) (hzâ‚€ : f zâ‚€ = 0) :
    p.order = 0 â†” âˆ€á¶  z in ğ“ zâ‚€, f z = 0 := by
  rw [hp.locally_zero_iff]
  by_cases p = 0
  case pos => simp [h]
  case neg =>
    simp [FormalMultilinearSeries.order_eq_zero_iff h, h]
    ext1
    rw [hp.coeff_zero, hzâ‚€]; rfl

lemma order_pos_iff {p : FormalMultilinearSeries â„‚ â„‚ â„‚}
    (hp : HasFPowerSeriesAt f p zâ‚€) (hzâ‚€ : f zâ‚€ = 0) :
    0 < p.order â†” âˆƒá¶  z in ğ“ zâ‚€, f z â‰  0 := by
  simp [pos_iff_ne_zero, (order_eq_zero_iff hp hzâ‚€).not]

lemma cindex_pos (h1 : AnalyticAt â„‚ f zâ‚€) (h2 : f zâ‚€ = 0) (h3 : âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z â‰  0) :
    âˆ€á¶  r in ğ“[>] 0, cindex zâ‚€ r f â‰  0 := by
  obtain âŸ¨p, hpâŸ© := h1
  filter_upwards [cindex_eventually_eq_order hp] with r h4
  simpa [h4, order_eq_zero_iff hp h2] using h3.frequently.filter_mono nhdsWithin_le_nhds

-- TODO: this can be generalized a lot
lemma hurwitz2_1 {K : Set â„‚} (hK : IsCompact K) (F_conv : TendstoUniformlyOn F f p K)
    (hf1 : ContinuousOn f K) (hf2 : âˆ€ z âˆˆ K, f z â‰  0) :
    âˆ€á¶  n in p, âˆ€ z âˆˆ K, F n z â‰  0 := by
  by_cases K = âˆ…
  case pos => simp [h]
  case neg =>
    obtain âŸ¨zâ‚€, h1, h2âŸ© : âˆƒ zâ‚€ âˆˆ K, âˆ€ z âˆˆ K, â€–f zâ‚€â€– â‰¤ â€–f zâ€– :=
      hK.exists_forall_le (nonempty_iff_ne_empty.2 h) (continuous_norm.comp_continuousOn hf1)
    have h3 := tendstoUniformlyOn_iff.1 F_conv (â€–f zâ‚€â€–) (norm_pos_iff.2 (hf2 _ h1))
    filter_upwards [h3] with n hn z hz h
    specialize hn z hz
    specialize h2 z hz
    simp [h] at hn h2
    linarith

lemma TendstoUniformlyOn.tendsto_circle_integral (hr : 0 < r)
    (F_cont : âˆ€á¶  n in p, ContinuousOn (F n) (sphere zâ‚€ r))
    (F_conv : TendstoUniformlyOn F f p (sphere zâ‚€ r)) :
    Filter.Tendsto (Î» i => âˆ® z in C(zâ‚€, r), F i z) p (ğ“ (âˆ® z in C(zâ‚€, r), f z))
    := by
  by_cases NeBot p
  case neg => simp at h; simp [h]
  case pos =>
    have f_cont : ContinuousOn f (sphere zâ‚€ r) := F_conv.continuousOn F_cont
    rw [Metric.tendsto_nhds]
    intro Îµ hÎµ
    have twopir_ne_zero : 2 * Real.pi * r â‰  0 := by simp [Real.pi_ne_zero, hr.ne.symm]
    have : (2 * Real.pi * r)â»Â¹ * Îµ > 0 :=
      mul_pos (inv_pos.mpr (mul_pos (mul_pos two_pos Real.pi_pos) hr)) hÎµ.lt
    filter_upwards [tendstoUniformlyOn_iff.mp F_conv ((2 * Real.pi * r)â»Â¹ * Îµ) this, F_cont] with n h h'
    simp_rw [dist_comm (f _) _, Complex.dist_eq, â† Complex.norm_eq_abs] at h
    rw [Complex.dist_eq, â† circleIntegral.sub hr.le h' f_cont, â† Complex.norm_eq_abs]
    have : âˆƒ x âˆˆ sphere zâ‚€ r, â€–F n x - f xâ€– < (2 * Real.pi * r)â»Â¹ * Îµ := by
      have : zâ‚€ + r âˆˆ sphere zâ‚€ r := by simp [hr.le, Real.norm_eq_abs]
      exact âŸ¨zâ‚€ + r, this, h _ thisâŸ©
    convert circleIntegral.norm_integral_lt_of_norm_le_const_of_lt hr (h'.sub f_cont) (Î» z hz => (h z hz).le) this
    field_simp [hr.ne, Real.pi_ne_zero, two_ne_zero]; ring

lemma hurwitz2_2 (hU : IsOpen U) (hF : âˆ€á¶  n in p, DifferentiableOn â„‚ (F n) U)
    (hf : TendstoLocallyUniformlyOn F f p U) (hr1 : 0 < r) (hr2 : sphere zâ‚€ r âŠ† U)
    (hf1 : âˆ€ (z : â„‚), z âˆˆ sphere zâ‚€ r â†’ f z â‰  0) :
    Tendsto (cindex zâ‚€ r âˆ˜ F) p (ğ“ (cindex zâ‚€ r f)) := by
  by_cases NeBot p
  case neg => simp at h; simp [h]
  case pos =>
    have H1 : IsCompact (sphere zâ‚€ r) := isCompact_sphere zâ‚€ r
    have H2 : TendstoUniformlyOn F f p (sphere zâ‚€ r) :=
      (tendstoLocallyUniformlyOn_iff_forall_isCompact hU).1 hf _ hr2 H1
    have H3 : DifferentiableOn â„‚ f U := hf.differentiableOn hF hU
    have H4 : ContinuousOn f (sphere zâ‚€ r) := H3.continuousOn.mono hr2
    have H5 : âˆ€á¶  n in p, ContinuousOn (F n) (sphere zâ‚€ r) := by
      filter_upwards [hF] with n h using h.continuousOn.mono hr2
    have H6 : âˆ€á¶  n in p, ContinuousOn (deriv (F n)) (sphere zâ‚€ r) := by
      filter_upwards [hF] with n h using (h.deriv hU).continuousOn.mono hr2
    have H7 : TendstoUniformlyOn (deriv âˆ˜ F) (deriv f) p (sphere zâ‚€ r) :=
      (tendstoLocallyUniformlyOn_iff_forall_isCompact hU).1 (hf.deriv hF hU) _ hr2 H1
    have H8 : ContinuousOn (deriv f) (sphere zâ‚€ r) :=
      (H3.deriv hU).continuousOn.mono hr2
    refine Tendsto.const_mul _ (TendstoUniformlyOn.tendsto_circle_integral hr1 ?_ ?_)
    { filter_upwards [hurwitz2_1 H1 H2 H4 hf1, H6, H5] with n hn H6 H5 using ContinuousOn.div H6 H5 hn }
    { exact TendstoUniformlyOn.div_of_compact H7 H2 H8 H4 hf1 H1 }

lemma hurwitz2
    (hU : IsOpen U)
    (hF : âˆ€á¶  n in p, DifferentiableOn â„‚ (F n) U)
    (hf : TendstoLocallyUniformlyOn F f p U)
    (hr1 : 0 < r)
    (hr2 : closedBall zâ‚€ r âŠ† U)
    (hf1 : âˆ€ z âˆˆ sphere zâ‚€ r, f z â‰  0)
    (hf2 : cindex zâ‚€ r f â‰  0)
    :
    âˆ€á¶  n in p, âˆƒ z âˆˆ ball zâ‚€ r, F n z = 0
    := by
  by_cases NeBot p
  case neg => simp at h; simp [h]
  case pos =>
    have H1 : IsCompact (sphere zâ‚€ r) := isCompact_sphere zâ‚€ r
    have H2 : sphere zâ‚€ r âŠ† U := sphere_subset_closedBall.trans hr2
    have H3 : TendstoUniformlyOn F f p (sphere zâ‚€ r) :=
      (tendstoLocallyUniformlyOn_iff_forall_isCompact hU).1 hf _ H2 H1
    have H4 : ContinuousOn f (sphere zâ‚€ r) :=
      (hf.differentiableOn hF hU).continuousOn.mono H2
    have H5 : âˆ€á¶  n in p, âˆ€ z âˆˆ sphere zâ‚€ r, F n z â‰  0 := hurwitz2_1 H1 H3 H4 hf1
    filter_upwards [(hurwitz2_2 hU hF hf hr1 H2 hf1).eventually_ne hf2, H5, hF] with n h h' hF
    contrapose! h
    have : âˆ€ (z : â„‚), z âˆˆ ball zâ‚€ r âˆª sphere zâ‚€ r â†’ F n z â‰  0 := Î» z hz => hz.casesOn (h z) (h' z)
    refine cindex_eq_zero hU hr1 hr2 hF (by rwa [â† ball_union_sphere])

lemma hurwitz3
    (hU : IsOpen U)
    (hF : âˆ€á¶  n in p, DifferentiableOn â„‚ (F n) U)
    (hf : TendstoLocallyUniformlyOn F f p U)
    (hzâ‚€ : zâ‚€ âˆˆ U)
    (h1 : f zâ‚€ = 0)
    (h2 : âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z â‰  0)
    (hs : s âˆˆ ğ“ zâ‚€)
    :
    âˆ€á¶  n in p, âˆƒ z âˆˆ s, F n z = 0
    := by
  by_cases NeBot p
  case neg => simp at h; simp [h]
  case pos =>
    have H1 := (hf.differentiableOn hF hU).analyticAt (hU.mem_nhds hzâ‚€)
    have H5 := cindex_pos H1 h1 h2
    rw [eventually_nhdsWithin_iff] at h2
    have h3 := eventually_nhds_iff_eventually_closed_ball.1 h2
    have h4 : âˆ€á¶  r in ğ“[>] 0, closedBall zâ‚€ r âŠ† U :=
      (eventually_closedBall_subset (hU.mem_nhds hzâ‚€)).filter_mono nhdsWithin_le_nhds
    have h4' : âˆ€á¶  r in ğ“[>] 0, closedBall zâ‚€ r âŠ† s :=
      (eventually_closedBall_subset hs).filter_mono nhdsWithin_le_nhds
    obtain âŸ¨r, hr, h5, h6, h7, h9âŸ© := (h3.and (h4.and (H5.and h4'))).exists'
    have h8 : âˆ€ z âˆˆ sphere zâ‚€ r, f z â‰  0 := by
      exact Î» z hz => h5 z (sphere_subset_closedBall hz) (ne_of_mem_sphere hz hr.lt.ne.symm)
    refine (hurwitz2 hU hF hf hr h6 h8 h7).mono ?_
    rintro n âŸ¨z, hz, hFnzâŸ©
    refine âŸ¨z, h9 (ball_subset_closedBall hz), hFnzâŸ©

----------------

theorem local_hurwitz [NeBot p]
    (hU : IsOpen U)
    (F_holo : âˆ€á¶  n in p, DifferentiableOn â„‚ (F n) U)
    (F_noz : âˆ€ n, âˆ€ z âˆˆ U, F n z â‰  0)
    (F_conv : TendstoLocallyUniformlyOn F f p U)
    (hzâ‚€ : zâ‚€ âˆˆ U)
    (hfzâ‚€ : f zâ‚€ = 0)
    :
    âˆ€á¶  z in ğ“ zâ‚€, f z = 0 := by
  have H1 := (F_conv.differentiableOn F_holo hU).analyticAt (hU.mem_nhds hzâ‚€)
  cases H1.eventually_eq_zero_or_eventually_ne_zero
  case inl => assumption
  case inr h =>
    obtain âŸ¨pf, hpâŸ© := H1
    by_contra' hh
    rw [Filter.not_eventually] at hh
    have h1 := (order_pos_iff hp hfzâ‚€).2 hh
    obtain âŸ¨r, h1, h2, h3, h4âŸ© :
        âˆƒ r > 0, (closedBall zâ‚€ r âŠ† U) âˆ§ (âˆ€ z âˆˆ sphere zâ‚€ r, f z â‰  0) âˆ§ (cindex zâ‚€ r f â‰  0) := by
      rw [eventually_nhdsWithin_iff, eventually_nhds_iff_eventually_closed_ball] at h
      have h4 := cindex_eventually_eq_order hp
      have h5 : âˆ€á¶  r in ğ“[>] 0, closedBall zâ‚€ r âŠ† U :=
        (eventually_closedBall_subset (hU.mem_nhds hzâ‚€)).filter_mono nhdsWithin_le_nhds
      obtain âŸ¨r, h6, h7, h8, h9âŸ© := (h.and (h4.and h5)).exists'
      refine âŸ¨r, h6, h9, ?_, ?_âŸ©
      { exact Î» z hz => h7 z (sphere_subset_closedBall hz) (ne_of_mem_sphere hz h6.lt.ne.symm) }
      { simp [h8, h1.ne.symm] }
    obtain âŸ¨n, z, h5, h6âŸ© := (hurwitz2 hU F_holo F_conv h1 h2 h3 h4).exists
    cases F_noz n z (h2 (ball_subset_closedBall (mem_ball.mpr h5))) h6

theorem hurwitz [NeBot p]
    (hU : IsOpen U)
    (hU' : IsPreconnected U)
    (F_holo : âˆ€á¶  n in p, DifferentiableOn â„‚ (F n) U)
    (F_noz : âˆ€ n, âˆ€ z âˆˆ U, F n z â‰  0)
    (F_conv : TendstoLocallyUniformlyOn F f p U)
    (hzâ‚€ : zâ‚€ âˆˆ U)
    (hfzâ‚€ : f zâ‚€ = 0)
    :
    âˆ€ z âˆˆ U, f z = 0 := by
  have := local_hurwitz hU F_holo F_noz F_conv hzâ‚€ hfzâ‚€
  have h1 : DifferentiableOn â„‚ f U := F_conv.differentiableOn F_holo hU
  have h2 := h1.analyticOn hU
  exact h2.eqOn_zero_of_preconnected_of_eventuallyEq_zero hU' hzâ‚€ this

theorem hurwitz' [NeBot p]
    (hU : IsOpen U)
    (hU' : IsPreconnected U)
    (F_holo : âˆ€á¶  n in p, DifferentiableOn â„‚ (F n) U)
    (F_noz : âˆ€ n, âˆ€ z âˆˆ U, F n z â‰  0)
    (F_conv : TendstoLocallyUniformlyOn F f p U)
    :
    (âˆ€ z âˆˆ U, f z â‰  0) âˆ¨ (âˆ€ z âˆˆ U, f z = 0) := by
  refine or_iff_not_imp_left.mpr (Î» h => ?_)
  push_neg at h
  obtain âŸ¨zâ‚€, h1, h2âŸ© := h
  exact hurwitz hU hU' F_holo F_noz F_conv h1 h2

lemma hurwitz_1 (hU : IsOpen U) (hU' : IsPreconnected U) (hf : DifferentiableOn â„‚ f U) :
    (EqOn f 0 U) âˆ¨ (âˆ€ zâ‚€ âˆˆ U, âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z â‰  0) := by
  refine or_iff_not_imp_right.2 (Î» h => ?_)
  obtain âŸ¨zâ‚€, h1, h2âŸ© : âˆƒ zâ‚€ âˆˆ U, âˆƒá¶  z in ğ“[â‰ ] zâ‚€, f z = 0 := by simpa [not_forall] using h
  exact (hf.analyticOn hU).eqOn_zero_of_preconnected_of_frequently_eq_zero hU' h1 h2

lemma hurwitz4 [TopologicalSpace Î±] [UniformSpace Î²] [UniformSpace Î³]
    {F : Î¹ â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {Ï† : Î² â†’ Î³}
    (hf : TendstoLocallyUniformlyOn F f p U) (hÏ† : UniformContinuous Ï†) :
    TendstoLocallyUniformlyOn (Î» n => Ï† âˆ˜ F n) (Ï† âˆ˜ f) p U :=
  Î» _ hu z hz => hf _ (mem_map.1 (hÏ† hu)) z hz

theorem hurwitz_inj [NeBot p]
    (hU : IsOpen U)
    (hU' : IsPreconnected U)
    (hF : âˆ€á¶  n in p, DifferentiableOn â„‚ (F n) U)
    (hf : TendstoLocallyUniformlyOn F f p U)
    (hi : âˆƒá¶  n in p, InjOn (F n) U)
    :
    (âˆƒ w, âˆ€ z âˆˆ U, f z = w) âˆ¨ (InjOn f U)
    := by
  refine or_iff_not_imp_right.2 (Î» h => ?_)
  obtain âŸ¨x, hx, y, hy, hfxy, hxyâŸ© : âˆƒ x âˆˆ U, âˆƒ y âˆˆ U, f x = f y âˆ§ x â‰  y := by
    simp [InjOn] at h
    obtain âŸ¨x, h1, y, h2, h3, h4âŸ© := h
    refine âŸ¨x, h1, y, h3, h2, h4âŸ©
  --
  set g : â„‚ â†’ â„‚ := Î» z => f z - f x
  set G : Î¹ â†’ â„‚ â†’ â„‚ := Î» n z => F n z - f x
  have hG : âˆ€á¶  n in p, DifferentiableOn â„‚ (G n) U := by
    filter_upwards [hF] with n hF using hF.sub (differentiableOn_const _)
  have hg : TendstoLocallyUniformlyOn G g p U :=
    hurwitz4 hf (uniformContinuous_id.sub uniformContinuous_const)
  have hgx : g x = 0 := sub_self _
  have hgy : g y = 0 := by simp [hfxy]
  suffices : âˆ€ z âˆˆ U, g z = 0
  { exact âŸ¨f x, by simpa [sub_eq_zero] using thisâŸ© }
  --
  contrapose hi; simp only [not_frequently, InjOn, not_forall]
  have h1 : DifferentiableOn â„‚ g U := hg.differentiableOn hG hU
  have h2 : âˆ€ zâ‚€ âˆˆ U, âˆ€á¶  z in ğ“[â‰ ] zâ‚€, g z â‰  0 := (hurwitz_1 hU hU' h1).resolve_left hi
  obtain âŸ¨u, v, hu, hv, huvâŸ© := t2_separation_nhds hxy
  have h3 := hurwitz3 hU hG hg hx hgx (h2 x hx) (inter_mem hu (hU.mem_nhds hx))
  have h4 := hurwitz3 hU hG hg hy hgy (h2 y hy) (inter_mem hv (hU.mem_nhds hy))
  filter_upwards [h3.and h4] with n hn
  obtain âŸ¨âŸ¨xn, hxn, hGxnâŸ©, âŸ¨yn, hyn, hGynâŸ©âŸ© := hn
  refine âŸ¨xn, hxn.2, yn, hyn.2, ?_, huv.ne_of_mem hxn.1 hyn.1âŸ©
  rw [sub_eq_zero] at hGxn hGyn
  rw [hGxn, hGyn]
