import Mathlib.Tactic
import Mathlib.Analysis.Calculus.ContDiffDef
import Mathlib.Analysis.Calculus.Deriv.Basic
import Mathlib.MeasureTheory.Integral.IntervalIntegral
import Mathlib.Topology.LocallyConstant.Basic
import Mathlib.Analysis.Calculus.MeanValue
import RMT4.Subdivision

open BigOperators Metric Set Subdivision Topology Filter Nat

structure IsLocDerivOn (U : Set ‚ÑÇ) (f : ‚ÑÇ ‚Üí ‚ÑÇ) :=
  F : U ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ
  S : U ‚Üí Set ‚ÑÇ
  mem (z : U) : z.1 ‚àà S z
  opn (z : U) : IsOpen (S z)
  sub (z : U) : S z ‚äÜ U
  dif (z : U) : DifferentiableOn ‚ÑÇ (F z) (S z)
  eqd (z : U) : (S z).EqOn f (deriv (F z))

lemma IsLocDerivOn.nhd (h : IsLocDerivOn U f) (z : U) : h.S z ‚àà ùìù z.1 :=
  (h.opn z).mem_nhds (h.mem z)

noncomputable def isLocDerivOn_deriv (hU : IsOpen U) (hF : DifferentiableOn ‚ÑÇ F U) :
    IsLocDerivOn U (deriv F) where
  F _ := F
  S _ := U
  sub _ := by rfl
  mem z := z.2
  opn _ := hU
  eqd _ := eqOn_refl (deriv F) U
  dif _ := hF

section pintegral

noncomputable def pintegral (hab : a < b) (f : ‚ÑÇ ‚Üí ‚ÑÇ) (Œ≥ : ‚Ñù ‚Üí ‚ÑÇ) (h2 : MapsTo Œ≥ (Set.Icc a b) U)
    (hŒ≥ : ContinuousOn Œ≥ (Set.Icc a b)) (hf : IsLocDerivOn U f) : ‚ÑÇ :=
  have h1 (t : Set.Icc a b) : ‚àÉ i, hf.S i ‚àà ùìù (Œ≥ t) :=
    let u : U := ‚ü®Œ≥ t, h2 t.2‚ü©
    ‚ü®u, hf.nhd u‚ü©
  let RW := exists_reladapted hab hŒ≥ h1
  RW.œÉ.sumSubAlong (hf.F ‚àò RW.I) Œ≥

end pintegral

lemma isLocallyConstant_of_deriv_eq_zero (hU : IsOpen U) {f : ‚ÑÇ ‚Üí ‚ÑÇ} (h : DifferentiableOn ‚ÑÇ f U)
    (hf : U.EqOn (deriv f) 0) :
    IsLocallyConstant (U.restrict f) := by
  refine (IsLocallyConstant.iff_exists_open _).2 (Œª ‚ü®z, hz‚ü© => ?_)
  obtain ‚ü®Œµ, L1, L2‚ü© := isOpen_iff.1 hU z hz
  refine ‚ü®ball ‚ü®z, hz‚ü© Œµ, isOpen_ball, mem_ball_self L1, Œª ‚ü®z', _‚ü© hz' => ?_‚ü©
  refine (convex_ball z Œµ).is_const_of_fderivWithin_eq_zero (h.mono L2) ?_ hz' (mem_ball_self L1)
  intro x hx
  rw [fderivWithin_eq_fderiv (isOpen_ball.uniqueDiffWithinAt hx)]
  ¬∑ exact ContinuousLinearMap.ext_ring (hf (L2 hx))
  ¬∑ exact h.differentiableAt (hU.mem_nhds (L2 hx))

lemma apply_eq_of_path (hab : a ‚â§ b) {f : ‚ÑÇ ‚Üí ‚ÑÇ} (hf : IsLocallyConstant (U.restrict f))
    {Œ≥ : ‚Ñù ‚Üí ‚ÑÇ} (hŒ≥ : ContinuousOn Œ≥ (Set.Icc a b)) (h : MapsTo Œ≥ (Set.Icc a b) U) :
    f (Œ≥ b) = f (Œ≥ a) := by
  haveI : PreconnectedSpace (Set.Icc a b) := isPreconnected_iff_preconnectedSpace.1 isPreconnected_Icc
  have h2 := hf.comp_continuous (hŒ≥.restrict_mapsTo h)
  exact @IsLocallyConstant.apply_eq_of_isPreconnected _ _ _ _ (h2) _ isPreconnected_univ
    ‚ü®b, hab, le_rfl‚ü© ‚ü®a, le_rfl, hab‚ü© (mem_univ _) (mem_univ _)

lemma sumSubAlong_eq_zero (hab : a ‚â§ b) {DW : IsLocDerivOn U 0}
  {RW : reladapted a b DW.S Œ≥} (hŒ≥ : ContinuousOn Œ≥ (Set.Icc a b)) :
    RW.œÉ.sumSubAlong (DW.F ‚àò RW.I) Œ≥ = 0 := by
  refine Subdivision.sum_eq_zero (Œª k => (sub_eq_zero.2 ?_))
  apply apply_eq_of_path (RW.œÉ.mono' hab)
  ¬∑ apply isLocallyConstant_of_deriv_eq_zero (DW.opn _) (DW.dif _)
    exact Œª _ hz => (DW.eqd (RW.I k) hz).symm
  ¬∑ exact hŒ≥.mono (RW.œÉ.Icc_subset hab)
  ¬∑ exact mapsTo'.2 (RW.sub k)

lemma pintegral_zero (hab : a < b) : pintegral hab 0 Œ≥ h2 hŒ≥ hf = 0 := by
  simp [pintegral, sumSubAlong_eq_zero hab.le hŒ≥]

lemma sub_eq_sub_of_deriv_eq_deriv (hab : a ‚â§ b) (hU : IsOpen U)
    {Œ≥ : ‚Ñù ‚Üí ‚ÑÇ} (hŒ≥‚ÇÅ : ContinuousOn Œ≥ (Set.Icc a b)) (hŒ≥‚ÇÇ : MapsTo Œ≥ (Set.Icc a b) U)
    {f g : ‚ÑÇ ‚Üí ‚ÑÇ} (hf : DifferentiableOn ‚ÑÇ f U) (hg : DifferentiableOn ‚ÑÇ g U)
    (hfg : ‚àÄ z ‚àà U, deriv f z = deriv g z) :
    f (Œ≥ b) - f (Œ≥ a) = g (Œ≥ b) - g (Œ≥ a) := by
  rw [sub_eq_sub_iff_sub_eq_sub]
  change (f - g) (Œ≥ b) = (f - g) (Œ≥ a)
  refine apply_eq_of_path (U := U) hab ?_ hŒ≥‚ÇÅ hŒ≥‚ÇÇ
  refine isLocallyConstant_of_deriv_eq_zero hU (hf.sub hg) (Œª z hz => ?_)
  have h1 : DifferentiableAt ‚ÑÇ f z := hf.differentiableAt (hU.mem_nhds hz)
  have h2 : DifferentiableAt ‚ÑÇ g z := hg.differentiableAt (hU.mem_nhds hz)
  have h3 : deriv (f - g) z = deriv f z - deriv g z := deriv_sub h1 h2
  simp [hfg z hz, h3]

lemma sumSubAlong_eq_of_sigma (hab : a ‚â§ b) {hf : IsLocDerivOn U f} {RW‚ÇÅ RW‚ÇÇ : reladapted a b hf.S Œ≥}
    (h : RW‚ÇÅ.œÉ = RW‚ÇÇ.œÉ) (hŒ≥ : ContinuousOn Œ≥ (Set.Icc a b)) :
    RW‚ÇÅ.œÉ.sumSubAlong (hf.F ‚àò RW‚ÇÅ.I) Œ≥ = RW‚ÇÇ.œÉ.sumSubAlong (hf.F ‚àò RW‚ÇÇ.I) Œ≥ := by
  rcases hf with ‚ü®F, S, _, Sopn, _, Sdif, Seqd‚ü©
  rcases RW‚ÇÅ with ‚ü®œÉ, I‚ÇÅ, hI‚ÇÅ‚ü©
  rcases RW‚ÇÇ with ‚ü®œÉ', I‚ÇÇ, hI‚ÇÇ‚ü©
  subst h
  refine Subdivision.sum_congr (Œª k => ?_)
  apply sub_eq_sub_of_deriv_eq_deriv (œÉ.mono' hab) ((Sopn _).inter (Sopn _))
  ¬∑ exact (hŒ≥.mono (œÉ.Icc_subset hab))
  ¬∑ simpa only [mapsTo'] using subset_inter (hI‚ÇÅ k) (hI‚ÇÇ k)
  ¬∑ exact (Sdif _).mono (inter_subset_left _ _)
  ¬∑ exact (Sdif _).mono (inter_subset_right _ _)
  ¬∑ exact Œª z hz => (Seqd _ hz.1).symm.trans (Seqd _ hz.2)
